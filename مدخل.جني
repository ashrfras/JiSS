ئورد جيسس؛
ئورد ئقرئمجلد، ئقرئملف، ئكتبملف من ئساسية.خلفي.نف؛
ئورد الكل ك ئجرائية من ئساسية.خلفي.ئجرائية؛
ئورد الكل ك مسار من ئساسية.خلفي.نف.مسار؛

#يستطوف جميع ملفين ضمن المسار المعطا
#ويستولد جيسس لكل واحد منهم
#ويستجمع الحاصل في ملف سيسس
#يضعه في ئصل المسار المعطا
ئعلن مدخل () ك وعد فارغ:
	جيسس جيس = جيسس()؛
	# يقرئ الملفين في المسار المعطا
	نصية[] معطيين = ئجرائية.ردمعطيين()؛
	ئدا معطيين.طول <= 2:
		قل 'يرجا تحديد مسار مشروع جني'؛
		ئجرائية.ئخرج()؛
	تم
	نصية مسارمجلد = معطيين[2]؛
	نصية[] ملفين = ريتما ئقرئمجلد(مسار: مسارمجلد، عودي: صحيح)؛
	ملفين = ملفين.رششح(دالة (نصية عنصر): عنصر.ينتهيب('.جني') ئو عنصر.ينتهيب('.html'))؛
	
	# لكل ملف يستدعي جيسس.ئستولد()
	نصية نتيجة = ''؛
	لكل ئسملف في ملفين:
		نصية مساري = مسار.ئدمج([مسارمجلد، ئسملف])؛
		نصية محتواه = ريتما ئقرئملف(مساري)؛
		ريتما جيس.ئستولد(محتواه)؛
		# يكتب النتيجة في ملف ستايل
		نصية مكتوب = "/*_{ئسملف}*/\n_{جيس.ردنتيجة()}\n"؛
		نتيجة = نتيجة + مكتوب؛
	تم
	ئكتبملف(مسار.ئدمج([مسارمجلد، 'style.css'])، نتيجة، 'كش')؛
تم
